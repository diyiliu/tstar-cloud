group 'com.tiza'
version '1.0'

apply plugin: 'java'

sourceCompatibility = 1.7

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile project(':star-common')

    compile fileTree(dir: rootDir.path + "/lib/tstar/common", include: "*.jar")
    compile fileTree(dir: rootDir.path + "/lib/tstar/process", include: "*.jar")
}

// 拷贝文件(排除项目自带jar)
task copyJar(type: Copy) {
    from configurations.runtime
    into "$buildDir/dist"
    exclude {
        //detail -> detail.file.path.contains('tstar-cloud\\lib')
        it.file.path.contains('tstar-cloud\\lib\\tstar')
    }
}

jar {
    archiveName = 'mstar_tiza.jar'
    from {
        File srcDir = file("$buildDir/dist")
        // 使用闭合创建一个文件集合
        FileCollection collection = files { srcDir.listFiles() }
        collection.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
jar.dependsOn(copyJar)

// 拷贝jar(适配不同的协议类型)
task copyZt(type: Copy, dependsOn: jar) {
    from "$buildDir/libs/mstar_tiza.jar"
    into "$buildDir/libs/"
    rename {
        fileName -> 'mstar_zt.jar'
    }
}

// 拷贝tstar配置文件
task copyConf(type: Copy) {
    from "$projectDir/src/main/conf"
    into "$buildDir/libs/conf"
}

// 打包
task zipM2(type: Zip, dependsOn: [copyConf, jar]) {
    baseName 'mstar_tiza'
    from("$buildDir/libs") {
        include "conf/$baseName-*"
        include "$baseName" + '.jar'
    }
}

task zipZt(type: Zip, dependsOn: [copyConf, copyZt]) {
    baseName 'mstar_zt'
    from("$buildDir/libs") {
        include "conf/$baseName-*"
        include "$baseName" + '.jar'
    }
}

// 设置编码
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}